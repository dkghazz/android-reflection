invoke android.view.ViewGroup removeAllViews with root

// Print Log
invoke android.util.Log d java.lang.String::"ttt" java.lang.String::"here" with null

// Make TextView & set click listener
construct android.widget.TextView android.content.Context::this assign textView
invoke android.widget.TextView setText java.lang.CharSequence::"My_Name_is_textView" with textView
invoke android.widget.TextView setId int::123 with textView
invoke android.widget.TextView setTextSize float::30 with textView
construct androidx.constraintlayout.widget.ConstraintLayout$LayoutParams int::-2 int::-2 assign params
invoke androidx.constraintlayout.widget.ConstraintLayout$LayoutParams setMargins int::100 int::100 int::100 int::100 with params
set androidx.constraintlayout.widget.ConstraintLayout$LayoutParams leftToLeft int::0 with params
set androidx.constraintlayout.widget.ConstraintLayout$LayoutParams topToTop int::0 with params
invoke android.view.ViewGroup addView android.view.View::textView android.view.ViewGroup$LayoutParams::params with root
implement android.view.View$OnClickListener onClick
    invoke android.widget.Toast makeText android.content.Context::this java.lang.CharSequence::"HiAaron2" int::0 with null assign toast2
    invoke android.widget.Toast show with toast2
assign listener
invoke android.widget.TextView setOnClickListener android.view.View$OnClickListener::listener with textView
invoke android.widget.Toast makeText android.content.Context::this java.lang.CharSequence::"HiAaron" int::0 with null assign toast
invoke android.widget.Toast show with toast

// Request permissions
invoke android.app.Activity requestPermissions [Ljava.lang.String;::{"android.permission.READ_EXTERNAL_STORAGE","android.permission.READ_MEDIA_IMAGES","android.permission.POST_NOTIFICATIONS"} int::0 with this

// Make 2nd TextView & set color
construct android.widget.TextView android.content.Context::this assign textView2
invoke android.widget.TextView setText java.lang.CharSequence::"My_Name_is_textView2" with textView2
invoke android.widget.TextView setId int::1 with textView2
invoke android.widget.TextView setTextSize float::24 with textView2
invoke android.widget.TextView setTextColor int::-9711936 with textView2
invoke android.widget.TextView setMaxLines int::1 with textView2
invoke android.widget.TextView setEllipsize android.text.TextUtils$TruncateAt::END with textView2
construct androidx.constraintlayout.widget.ConstraintLayout$LayoutParams int::0 int::-2 assign params2
invoke androidx.constraintlayout.widget.ConstraintLayout$LayoutParams setMargins int::100 int::20 int::0 int::100 with params2
set androidx.constraintlayout.widget.ConstraintLayout$LayoutParams leftToLeft int::0 with params2
set androidx.constraintlayout.widget.ConstraintLayout$LayoutParams rightToRight int::0 with params2
set androidx.constraintlayout.widget.ConstraintLayout$LayoutParams topToBottom int::123 with params2
invoke android.view.ViewGroup addView android.view.View::textView2 android.view.ViewGroup$LayoutParams::params2 with root

// Make ImageView & set image with Glide
construct android.widget.ImageView android.content.Context::this assign imageView
invoke android.widget.ImageView setId int::2 with imageView
construct androidx.constraintlayout.widget.ConstraintLayout$LayoutParams int::400 int::200 assign params3
invoke androidx.constraintlayout.widget.ConstraintLayout$LayoutParams setMargins int::100 int::20 int::0 int::100 with params3
set androidx.constraintlayout.widget.ConstraintLayout$LayoutParams leftToLeft int::0 with params3
set androidx.constraintlayout.widget.ConstraintLayout$LayoutParams topToBottom int::1 with params3
invoke android.view.ViewGroup addView android.view.View::imageView android.view.ViewGroup$LayoutParams::params3 with root

invoke com.bumptech.glide.Glide with android.view.View::imageView with null assign glide
invoke com.bumptech.glide.RequestManager load java.lang.String::"https://filtermaker.fr/wp-content/uploads/2022/01/thinkaboutit-smart.gif" with glide assign requestBuilder
invoke com.bumptech.glide.RequestBuilder into android.widget.ImageView::imageView with requestBuilder

// Make PopupWindow
construct androidx.cardview.widget.CardView android.content.Context::this assign cardView
construct android.widget.TextView android.content.Context::this assign textView3
invoke android.widget.TextView setText java.lang.CharSequence::"This_is_PopupWindow" with textView3
invoke android.widget.TextView setTextSize float::14 with textView3
invoke android.widget.TextView setPadding int::20 int::20 int::20 int::20 with textView3
invoke androidx.cardview.widget.CardView addView android.view.View::textView3 with cardView
invoke androidx.cardview.widget.CardView setCardBackgroundColor int::-33333 with cardView
invoke androidx.cardview.widget.CardView setRadius float::20 with cardView
invoke androidx.cardview.widget.CardView setCardElevation float::10 with cardView
construct android.widget.PopupWindow android.view.View::cardView int::-2 int::-2 assign popupWindow
implement android.view.View$OnClickListener onClick
    invoke android.widget.PopupWindow showAsDropDown android.view.View::imageView int::0 int::20 with popupWindow
    invoke android.widget.TextView setOnClickListener android.view.View$OnClickListener::listener3 with imageView
assign listener2
implement android.view.View$OnClickListener onClick
    invoke android.widget.PopupWindow dismiss with popupWindow
    invoke android.widget.TextView setOnClickListener android.view.View$OnClickListener::listener2 with imageView
assign listener3
invoke android.widget.TextView setOnClickListener android.view.View$OnClickListener::listener2 with imageView

// Make Button & set click listener to animate
construct android.widget.Button android.content.Context::this assign button
invoke android.widget.Button setText java.lang.CharSequence::"Click_Me" with button
invoke android.widget.Button setId int::3 with button
construct androidx.constraintlayout.widget.ConstraintLayout$LayoutParams int::-2 int::-2 assign params4
invoke androidx.constraintlayout.widget.ConstraintLayout$LayoutParams setMargins int::100 int::20 int::0 int::100 with params4
set androidx.constraintlayout.widget.ConstraintLayout$LayoutParams leftToLeft int::0 with params4
set androidx.constraintlayout.widget.ConstraintLayout$LayoutParams topToBottom int::2 with params4
invoke android.view.ViewGroup addView android.view.View::button android.view.ViewGroup$LayoutParams::params4 with root

implement android.view.View$OnClickListener onClick
    construct android.view.animation.AlphaAnimation float::0.0 float::1.0 assign fadeIn
    invoke android.view.animation.AlphaAnimation setDuration long::1000 with fadeIn
    invoke android.widget.TextView startAnimation android.view.animation.Animation::fadeIn with textView
assign listener4
invoke android.widget.TextView setOnClickListener android.view.View$OnClickListener::listener4 with button

// Make notification channel
invoke android.content.Context getSystemService java.lang.String::"notification" with this assign notificationManager
construct android.app.NotificationChannel java.lang.String::"channel_id" java.lang.CharSequence::"channel_name" int::4 assign channel
invoke android.app.NotificationChannel enableLights boolean::true with channel
invoke android.app.NotificationChannel enableVibration boolean::true with channel
invoke android.app.NotificationManager createNotificationChannel android.app.NotificationChannel::channel with notificationManager

// Send notification
construct android.app.Notification$Builder android.content.Context::this java.lang.String::"channel_id" assign builder
invoke android.app.Notification$Builder setContentTitle java.lang.CharSequence::"Title" with builder
invoke android.app.Notification$Builder setContentText java.lang.CharSequence::"Content" with builder
invoke android.content.Context getResources with this assign res
invoke android.content.res.Resources getIdentifier java.lang.String::"ic_launcher" java.lang.String::"mipmap" java.lang.String::"com.example.myapplication" with res assign icon
invoke android.app.Notification$Builder setSmallIcon int::icon with builder
invoke android.app.Notification$Builder setPriority int::1 with builder
invoke android.app.Notification$Builder setDefaults int::-1 with builder
invoke android.app.Notification$Builder setShowWhen boolean::true with builder
invoke android.app.Notification$Builder build with builder assign notification
invoke android.app.NotificationManager notify int::1 android.app.Notification::notification with notificationManager
